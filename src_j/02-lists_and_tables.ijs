table =: 2 3 $ 5 6 7 8 9 10 NB. 構建2行3列的表格，左側給出大小，右側給出值
table 1!:2 (2)
table =: 2 3 $ 5 6 7 8 NB. 用列表循環填充表格
table 1!:2 (2)

report =: 2 2 3 $ 5 6 7 8 NB. 三維數組，是列表與表格的擴展
report 1!:2 (2)
($ report) 1!:2 (2) NB. 單目 $ 給出參數維度列表
(# $ report) 1!:2 (2) NB. # $ 結合，得出數組維度
($ 7) 1!:2 (2) NB. 空列表，對應0維數組
(# $ 7) 1!:2 (2) NB. 因爲列表爲空，數組維度也爲0
NB. “數組維度” 又被稱爲 “秩” rank。

title =: 'My Ten Years in a Quandary' NB. 字符列表如同單引號字符串。
title 1!:2 (2)
'What''s new?' 1!:2 (2) NB. 單引號轉義

a =: 'rear'
b =: 'ranged'
(a,b) 1!:2 (2) NB. Append 拼接數組

Y =: 'abcd'
(0 { Y) 1!:2 (2) NB. 以下標從列表中選擇項目, "From"
(i. 4) 1!:2 (2) NB. 生成n個從零開始的連續自然數
(i. # Y) 1!:2 (2) NB. 生成數組Y的所有下標
('park' i. 'k') 1!:2 (2) NB. 搜索項目 Index of
('park' i. 'j') 1!:2 (2) NB. 如果找不到，就越界

X =: 'abc'
(X -: X) 1!:2 (2) NB. 判斷兩個數組是否相同
NB. 具體而言，形狀相同且對應元素相同
('' -: 0 $ 0) 1!:2 (2) NB. 空字符列表也等於空數字列表
(X = X) 1!:2 (2) NB. 一一判斷對應元素，返回與參數相同形狀的布爾數組

A =: 'The answer is' ; 42 NB. 連接異構的元素，形成“盒子”。
A 1!:2 (2) NB. 一個盒子視作一個標量

n =: 42
s =: ": n NB. 類型轉換 "Format" ：數字轉字符串
('The answer is ',s) 1!:2 (2) NB. 它們都是字符串

b =: (< 'baked beans') NB. "Box" 手動產生單個盒子
b 1!:2 (2)
(> b) 1!:2 (2) NB. "Open" 打開盒子